\ assembler macros

{: torax
rbx 8 sub_rr,s
rax rbx mov_rr,[rr]
}

{: fromrax
rbx rax mov_[rr],rr
rbx 8 add_rr,s
}

{: binary
rcx rax mov_rr,rr
torax
}

{: binary_cmp
binary
rax rcx cmp_rr,rr
}

\ pointer to cross-compiled vocabulary
create xcurrent 0 w,

{: xlink ( a -- a )
8 align
{ 0<> $ swap }~ a,
{ $ swap }~ q,
}~|

{: xlast ( -- a )
xcurrent }~ w@

{: xtraverse& ( a -( fail pend )- a )
w@ 0<>
offset @ +
'self &
dup
8 +
}

{: xsearch ( a -( pad fail )- a )
xtraverse& 10 +
pad s= cut }~ nip

{: xfind ( -( pad fail )- a )
xcurrent xsearch 10 }~ -

\ target tick
{: x'
word xfind }~ w@

\ target pointer to callcomp
0 variable callcomp

\ target pointer to endcomp
0 variable endcomp

\ target pointer to execute
0 variable exec

\ target create
{: xcreate
xlast
xlink
xcurrent w!
here
0 a,
callcomp @ offset @ +
a,
word
pad
s,
0 c,
$ swap w!
}

\ target literal
{: xliteral ( x -( heap )- )
fromrax
rax swap mov_rr,xx
}

\ target address literal
{: xaliteral ( a -( heap )- )
fromrax
rax swap mov_rr,aa
}

{: x{{ ( -( heap )- a )
1FFFF call }~ here

{: x}} ( a -( heap )- )
dup here swap - swap 4 - w!
fromrax
rax pop
}

\ create association
{: assoc ( key value table -( heap )- )
dup @ link swap !
swap , , }

\ recall association
{: recall ( key table -- value )
traverse& third over @ s= cut drop drop nip cell+ cell+ @ }

\ compile tail call to self
{: tail jmp }~fail

\ target host associations
0 variable xcomps

\ ends of computation

" }"		{ drop ret }~fail	xcomps assoc
" }~"		{ drop word xfind
		w@ jmp }~fail		xcomps assoc
" }~fail"	' tail			xcomps assoc
" }~|"		' tail			xcomps assoc
" }~self"	{ drop dup
		offset @ - jmp }~fail	xcomps assoc
" 'id}~|"	' tail			xcomps assoc

\ comments

" \\"		{ drop }~ \		xcomps assoc
" ("		{ drop }~ (		xcomps assoc

\ unnamed macros

" ["		{ drop ' interpret
		'id}~|			xcomps assoc


\ literal constructors

" ascii"	{ drop word pad
		c@ xliteral }		xcomps assoc
" '"		{ drop word xfind
		w@ xaliteral }		xcomps assoc
" 'self"	{ drop dup
		offset @ - xaliteral }	xcomps assoc

\ while loop
{: while
>r
dup >r
{ r> drop r> drop }~ } |
r> r> swap
>r dup >r
execute
r> r> swap
}~self

\ cross-compiler
{: xcompile
word
{ pad c@ 0= drop }
{ input word }
while
{ xfind w@
  { pad xcomps recall }
  { ' call }
}~|
{ pad s>number ' xliteral }
|

execute
}~self

\ cross string literal
{: xlits
bite
drop
{ bite ascii " <>
        { ascii \ = drop ' bite { A } }~|
        'id| c,
}~self
{ 0 }~ c,
}~|


\ quoted literals
" {"		{ drop 
		x{{ ' xcompile 'id | }~ x}}	xcomps assoc
" \""		{ drop x{{ xlits }~ x}}		xcomps assoc

\ Offsets in ELF header

0 variable headersLength
0 variable f_EntryPoint
0 variable f_ProgramHeaders
0 variable f_SectionHeaders
0 variable f_SizeInFile
0 variable f_TotalFileSize
0 variable f_StringTableVA
0 variable f_StringTableOffset

{: 0c, 0 c, }

{: field here swap ! }

\ system variables
18000 constant SEED
18008 constant DP
18010 constant TIB
18018 constant FAIL
18020 constant ERR_SP
18028 constant CONTEXT
18030 constant CURRENT
18038 constant BASE
18039 constant EMIT_BUF
1803A constant PAD
   20 constant PAD_LEN
1805A constant TIB_BUF
  100 constant TIB_BUF_LEN

18160 constant EFFECTS

\ effect pointer for cross-compiling
EFFECTS variable effects

\ native word definition
{: native
xcreate
}

\ postpone colon definition
{: postpone
native
1FFFF jmp
}

\ effect definition
{: effect ( -- )
native
effects @ dup 8 + effects !
jmp_[aa]
}

\ constant definition
{: constant
native
fromrax
rax swap mov_rr,x
ret
}

\ address definition (because of 16-bit cells)
{: address
native
fromrax
rax swap mov_rr,aa
ret
}

\ change compile mode behavior
{: does
xlast
10 +
offset @ +
w! }

{: immediate
exec @ }~ does

\ alias
{: alias
xcreate
word
xfind
dup
w@ \ interpret mode behavior
swap
8 +
w@ \ compile mode behavior
does
xlast
8 +
offset @ +
w! }

\ target quotation
{: {
$ ' xcompile 'id}~|

\ postponed colon definition
{: {::
word xfind w@ offset @ + \ host address of jmp instruction
1+ \ host address of jmp relative address
dup
4 + \ host address of jmp anchor
here swap -
swap w!
} \ TODO: adjust interpret mode behavior address, too

\ colon definitions
{: {:
native
here
' xcompile
' drop
}~|

\ Beginning of cross-compilation
create ELF

0 org

\ Signature
7F c, ascii E c, ascii L c, ascii F c,

\ Class 2, 64-bit
2 c,

\ Little-endian
1 c,

\ ELF version
1 c,

\ OS ABI
0c,

\ Extended ABI
0 q,

\ Executable file type
2 w,

\ Target architecture x86_64
3E w,

\ Additional ELF version
1 d,

f_EntryPoint field

0 w, 1 w, 0 d,

f_ProgramHeaders field

0 q,

f_SectionHeaders field

0 q,

\ Unused flags
0 d,

\ Header size
40 w,

\ Program header entry size
38 w,

\ Number of program header entries
1 w,

\ Section header entry size
40 w,

\ Number of section header entries
4 w,

\ Index of string table section
3 w,

\ Program Headers

$ f_ProgramHeaders @ w!

\ Program header type: 1 = loadable segment
1 d,

\ Program header flags: 7 = read + write + execute
7 d,

\ Loadable segment offset in the file
0 q,

\ Virtual address 10000
0 w, 1 w, 0 d,

\ Physical address ???
0 w, 1 w, 0 d,

f_SizeInFile field
0 q,

\ Size in memory
0 w, 1 w, 0 d,

\ Alignment
8000 q,

\ Section Headers

$ f_SectionHeaders @ w!

\ null header
' 0c, 40 times

\ .text segment

\ Offset of .text in the table
1 d,

\ Loadable "bits" section
1 d,

\ Flags: 6 = allocated + executable
6 q,

\ Virtual address 10000
0 w, 1 w, 0 d,

\ Offset in the file
0 q,

f_TotalFileSize field
0 q,

\ Linked section index
0 d,

\ Info ???
0 d,

\ Alignment
10 q,

\ Section entry size
0 q,

\ .bss segment

\ Offset of .bss in the table
7 d,

\ Type: nobits
8 d,

\ Flags: writable, allocated, executable
7 q,

\ Virtual address
8000 w, 1 w, 0 d,

\ Offset in the file
0 q,

\ Section size
8000 q,

\ Linked section index
0 d,

\ Info ???
0 d,

\ Alignment
10 q,

\ Section entry size
0 q,



\ String Table Section
C d,

\ Type: string table
3 d,

\ No need to load it
0 q,

f_StringTableVA field
0 w, 1 w, 0 d,

f_StringTableOffset field
0 q,

\ Length
16 q,

\ Other stuff
0 d, 0 d, 1 q, 0 q,

$ f_EntryPoint @ w!

7 jmp_f \ jump over the initial dictionary

0 label \ get heap top
rax C mov_rr,x  \ brk
syscall
ret

1 label \ low-level heap allocation of rcx bytes
rax push
rbx push
rcx push
rdi 0 mov_rr,x
0 lb call
rcx pop
rcx DP add_rr,[aa]
rax rcx cmp_rr,rr
2 jnc_fs
rdi rcx mov_rr,rr
0 lb call
2 rslabel
rbx pop
rax pop
ret

$ callcomp !
rcx 5 mov_rr,x
1 lb call
rcx DP mov_rr,[aa]
rcx E8 mov_byte_[rr],x \ call
2 label
rcx inc_rr
rax rcx sub_rr,rr
rax 4 sub_rr,s
rcx rax mov_[rr],er
rcx 4 add_rr,s
DP rcx mov_[aa],rr
torax
ret

native endcomp
$ endcomp !
rcx 5 mov_rr,x
1 lb call
rcx DP mov_rr,[aa]
rcx E9 mov_byte_[rr],x \ jmp
2 lb call
2 jmp_f

native }~fail
2 rlabel
2 label
rcx FAIL mov_rr,aa
3 label
rsp ERR_SP mov_rr,[aa]	\ restore rsp
rdx pop			\ handler pointer in rdx
rcx rdx cmp_rr,rr	\ compare with current effect's
rbx pop			\ rbx = previous handler address
rax rdx mov_rr,[rr]
rdx rbx mov_[rr],rr	\ restore previous handler address
rbx rax mov_rr,rr	\ rbx = current handler address
rax pop			\ rax = previous ERR_SP
ERR_SP rax mov_[aa],rr	\ restore previous ERR_SP
3 lb jnz_s		\ next handler, if not found
rcx rbx mov_rr,rr	\ rcx = current handler address
rbx pop			\ restore data stack pointer
rax pop			\ restore TOS
rcx jmp_rr		\ continue with handler

x' endcomp does

native carry?
2 lb jc
ret

\ arithmetic filters

native 0=
rax rax or_rr,rr
2 lb jnz
ret

native 0<>
rax rax or_rr,rr
2 lb jz
ret

\ relations

native =
binary_cmp
2 lb jnz
ret

native <>
binary_cmp
2 lb jz
ret

native >
binary_cmp
2 lb jbe
ret

native >=
binary_cmp
2 lb jb
ret

native <
binary_cmp
2 lb jae
ret

native <=
binary_cmp
2 lb ja
ret

\ compare strings for equality
3 label
torax
rsi rbx mov_rr,[rr]
rdi rax mov_rr,rr
rcx rcx xor_rr,rr
4 label
rcx rsi cmp_rl,[rr]
5 jz_fs
rcx rdi cmp_rl,[rr]
6 jz_fs
cmpsb
4 lb jz_s
6 rslabel
stc
ret
5 rslabel
rcx rdi cmp_rl,[rr]
ret

native s=
3 lb call
2 lb jc
ret

native s<>
3 lb call
2 lb jnc
ret

\ heap manipulation

DP address dp

native here
fromrax
rax DP mov_rr,[aa]
ret

native ,
rcx 8 mov_rr,x
1 lb call
rcx DP mov_rr,[aa]
rcx rax mov_[rr],rr
rcx 8 add_rr,s
DP rcx mov_[aa],rr
torax
ret

native d,
rcx 4 mov_rr,x
1 lb call
rcx DP mov_rr,[aa]
rcx rax mov_[rr],er
rcx 4 add_rr,s
DP rcx mov_[aa],rr
torax
ret

native c,
rcx 1 mov_rr,x
1 lb call
rcx DP mov_rr,[aa]
rcx rax mov_[rr],rl
rcx inc_rr
DP rcx mov_[aa],rr
torax
ret

native @
rax rax mov_rr,[rr]
ret

native d@
rcx rax mov_er,[rr]
rax rax xor_rr,rr
rax rcx mov_er,er
ret

native c@
rcx rax mov_rl,[rr]
rax rax xor_rr,rr
rax rcx mov_rl,rl
ret

native !
binary
rcx rax mov_[rr],rr
torax
ret

native d!
binary
rcx rax mov_[rr],er
torax
ret

native c!
binary
rcx rax mov_[rr],rl
torax
ret

native allot
binary
1 lb jmp

\ end Seed execution

native bye
rax 3C mov_rr,x \ Exit program
rdi 0 mov_rr,x
syscall

\ constnats
TIB address tib
PAD address pad

\ I/O effects

native stdemit
EMIT_BUF rax mov_[aa],rl
torax
rax push
rbx push
rax 1 mov_rr,x
rdi 1 mov_rr,x
rsi EMIT_BUF mov_rr,aa
rdx 1 mov_rr,x
syscall
rax 1 cmp_rr,s
rbx pop
rax pop
lb 2 jnz
ret

effect emit ( -( emit fail )- )

native stdwrite
binary
rbx push
rsi rax mov_rr,rr \ buffer
rdx rcx mov_rr,rr \ length
rax 1 mov_rr,x \ write
rdi 1 mov_rr,x \ stdout
syscall
rbx pop
ret

effect write ( -( write )- )

native stdkey
rax push
rbx push
rax 0 mov_rr,x
rdi 0 mov_rr,x
rsi EMIT_BUF mov_rr,aa
rdx 1 mov_rr,x
syscall
rax rax or_rr,rr
rbx pop
rax pop
2 lb jz
fromrax
rax rax xor_rr,rr
rax EMIT_BUF mov_rl,[aa]
ret

effect key ( -( key fail )- c )

\ edit a line of text on stdin (quick and dirty)
native stdinput
rax push
rbx push
rsi TIB_BUF mov_rr,aa
TIB rsi mov_[aa],rr
3 label
rsi push
rax 0 mov_rr,x
rdi 0 mov_rr,x
rdx 1 mov_rr,x
syscall
rsi pop
rax 1 cmp_rr,s
4 jnz_fs 
rax rsi mov_rl,[rr]
rax A cmp_rr,s
5 jz_fs
rsi inc_rr
rcx TIB_BUF TIB_BUF_LEN + 1+ mov_rr,aa
rsi rcx cmp_rr,rr
3 lb jnz_s
4 rslabel
5 rslabel
rsi 0 mov_byte_[rr],x
rbx pop
rax pop
ret

effect input

\ stack manipulation primitives

native drop
rbx 8 sub_rr,s
rax rbx mov_rr,[rr]
ret

native nip
rbx 8 sub_rr,s
ret

native swap
binary
rbx rcx mov_[rr],rr
rbx 8 add_rr,s
ret

native dup
fromrax
ret

native over
binary
rbx 8 add_rr,s
rbx rcx mov_[rr],rr
rbx 8 add_rr,s
ret

native third
rcx rbx mov_rr,rr
fromrax
rcx 10 sub_rr,s
rax rcx mov_rr,[rr]
ret

native >r
binary
rcx rsp xchg_rr,[rr]
rcx jmp_rr

native r>
fromrax
rcx pop
rax pop
rcx jmp_rr

\ execution control

\ end of computation
native }
ret

{ c@ c, }~fail

does

\ reference to identity
native 'id
fromrax
rax x' } mov_rr,aa
ret

\ execute computation
native execute
$ exec !
binary
rcx jmp_rr

\ handle effect
native handle
binary			\ effect reference in rcx
rcx 3 add_rr,s
rdx rdx xor_rr,rr
rdx rcx mov_er,[rr]	\ handler pointer in rdx
3 label
rbx 10 sub_rr,s		\ data stack pointer to be saved
rcx rbx mov_rr,[rr]	\ TOS to be saved
rcx push		\ save TOS
rbx push		\ save data stack pointer
rbx 10 add_rr,s		\ current data stack pointer
rcx ERR_SP mov_rr,[aa]	\ ERR_SP in rcx
rcx push		\ save previous ERR_SP
rcx rdx mov_rr,[rr]	\ previous handler address in rcx
rcx push		\ save previous handler address
rdx push		\ save handler pointer
ERR_SP rsp mov_[aa],rr  \ set new ERR_SP
binary			\ handler reference in rcx
rdx rcx mov_[rr],rr	\ handler reference changed
x' execute call		\ execute code with effects
rcx pop			\ handler ptr in rcx
rdx pop			\ previous handler addr. in rdx
rcx rdx mov_[rr],rr	\ restore handler
rcx pop
ERR_SP rcx mov_[aa],rr	\ restore ERR_SP
rsp 10 add_rr,s		\ discard data stack pointer and TOS
ret

\ disjunction combinator
native |
rdx FAIL mov_rr,aa
3 lb jmp

\ tail disjunction
alias }~| |

x' endcomp does

\ disjunction with identity
native 'id|
x' 'id call
x' | jmp

\ tail disjunction with identity
alias 'id}~| 'id|

x' endcomp does

\ cut handler
native cut
rsi ERR_SP mov_rr,[aa]
rdi rsi mov_rr,rr
std
rcx rdi mov_rr,[rr]	\ handler ptr in rcx
rdi 8 add_rr,s
rdx rdi mov_rr,[rr]	\ previous handler reference in rdx
rdi 8 add_rr,s
rcx rdx mov_[rr],rr	\ restore previous handler
rcx rdi mov_rr,[rr]	\ previous ERR_SP in rcx
ERR_SP rcx mov_[aa],rr	\ restore previous ERR_SP
rdi 10 add_rr,s		\ rdi points to top of handler frame
rcx rsi mov_rr,rr
rcx rsp sub_rr,rr	\ length of stack to move
rep_movsb		\ cannot be zero due to return addr.
rsp rdi mov_rr,rr	\ set stack pointer to destination
ret

\ pend
native &
rsi pop			\ return address in rsi
rsi push
binary			\ failure handler in rcx
rax push		\ save TOS
rbx push		\ save data stack pointer
rdx ERR_SP mov_rr,[aa]	\ previous ERR_SP in rdx
rdx push		\ save previous ERR_SP
rdx FAIL mov_rr,[aa]	\ previous handler in rdx
rdx push		\ save previous handler
rdx FAIL mov_rr,aa
rdx push		\ save failure handler pointer
rdx rcx	mov_[rr],rr	\ set new handler
ERR_SP rsp mov_[aa],rr	\ set new ERR_SP
rcx 28 mov_rr,x
rcx push		\ save return address offset
3 label
rsi call_rr		\ call what follows
rcx pop
rcx 8 add_rr,s
rcx push
rcx rsp add_rr,rr
rsi rcx mov_rr,[rr]
3 lb jmp

\ arithmetic mappers

native 1-
rax dec_rr
ret

native 1+
rax inc_rr
ret

native cell-
rax 8 sub_rr,s
ret

native cell+
rax 8 add_rr,s
ret

native cells
rax 3 shl_rr,s

\ binary arithmetic primitives

native +
binary
rax rcx add_rr,rr
ret

native -
binary
rax rcx sub_rr,rr
ret

native u* ( n n -- l h )
binary
rcx mul_rr
fromrax
rax rdx mov_rr,rr
ret

native /mod ( n n -- n n )
binary
rdx rdx xor_rr,rr
rcx div_rr
rbx rdx mov_[rr],rr
rbx 8 add_rr,s
ret

\ binary bitwise logic primitives

native and
binary
rax rcx and_rr,rr
ret

native or
binary
rax rcx or_rr,rr
ret

native xor
binary
rax rcx xor_rr,rr
ret

postpone word

\ literal constructors

{: literal
48 c, 89 c, 03 c,
48 c, 43 c, C3 c, 08 c,
48 c, B8 c, }~ ,

{: >lit execute }~ literal

{: ascii word pad }~ c@

x' >lit does

postpone find

{: ' word find }~ @

x' >lit does

{: {{
E8 c, 0 d,
}~ here

{: }}
dup here swap - swap 4 - d!
59 c,
48 c, 89 c, 03 c,
48 c, 83 c, C3 c, 08 c,
58 c,
FF c, E1 }~ c,

\ tail call

native }~
2 lb jmp \ not permitted in interpret mode

{ drop word find @ here - 5 + E9 c, d, }~fail

does

\ self references

\ tail call to self
native }~self
2 lb jmp \ not permitted in interpret mode

{ drop dup here - 5 + E9 c, d, }~fail

does

\ self reference
native 'self
2 lb jmp \ not permitted in interpret mode

{ drop dup }~ literal

does

postpone compile

\ quotation

{: quotate {{ execute }~ }}

{: { here ' compile 'id}~|

x' quotate does

\ quoted string literal

{: bite
tib @ dup c@ 0<> swap 1+ tib }~ !

{: "
here bite drop
{ bite ascii " <>
{ ascii \ = drop ' bite { A } }~|
'id|
c,
}~self
{ 0 }~ c,
}~|

x' quotate does

\ comments

\ skip the rest of the line
{: \ 0 tib @ }~ c!

\ skip till the closing paren
{: ( { word pad " )" s<> drop }~self 'id }~|

\ character filters

\ ascii code of space (a.k.a. blank)
bl constant bl

\ check whitespace
{: ws
bl }~ <=

\ check printable
{: printable
ascii ! >= ascii ~ }~ <=

\ decimal digit
{: ddigit
ascii 0 >= ascii 9 }~ <=

\ lowercase letter
{: lower
ascii a >= ascii z }~ <=

\ uppercase letter
{: upper
ascii A >= ascii Z }~ <=

\ letter
{: letter
' lower ' upper }~|

\ alphanumeric
{: alphanum
' ddigit ' letter }~|

\ character mappers

\ capitalize
{: >upper
{ lower DF }~ and
'id}~|

\ un-capitalize
{: >lower
{ upper 20 }~ or
'id}~|

\ arithmetics

{: * u* }~ drop

{: / /mod }~ nip

{: mod /mod }~ drop

\ numerals

BASE address base

\ set hexadecimal base
{: hex
10 base }~ c!

\ set decimal base
{: decimal
A base }~ c!

\ digit to integer conversion
{: digit>int
alphanum
ascii 0 -
carry?
{ 9 }~ <=
{ 7 }~ -
|
base c@ }~ <

\ auxiliary numeral conversion
{: >number
base c@ * over c@ digit>int + swap 1+ dup c@
{ ws drop }~ drop
{ drop swap }~ >number
}~|

\ string numeral to integer conversion
{: s>number
0 }~ >number

\ convert integer to digit
{: >digit
{ 9 <= ascii 0 }
{ 37 }
| }~ +

\ string functions

\ non-empty filter
{: nonempty
dup c@ 0<> }~ drop

\ string length
{: length
0 swap
{ nonempty 1+ swap 1+ swap }~self
' drop
}~|

\ discard leading whitespace
{: wsskip
{ dup c@ 0<> ws drop 1+ }~self
'id}~|

\ dictionary (a.k.a. heap) functions

\ put a string on the heap (w/o the trailing zero)
{: s,
{ dup c@ 0<> c, 1+ }~self
' drop
}~|

\ list functions

\ list extension
{: link
here swap }~ ,

\ list traversal
{: traverse&
@ 0<> 'self & dup }~ cell+

\ vocabulary functions

\ vocabulary in which words are searched
CONTEXT address context

\ vocabulary in which words are created
CURRENT address current

\ last word in the current vocabulary
{: last
current @ }~ @

\ make current vocabulary the same as the context vocabulary
{: definitions
context @ current }~ !

\ create new word returning the payload address


\ I/O functions

{: type
dup length write }~ drop

\ emit new line (a.k.a. carriage return)
{: cr
A }~ emit

\ emit space
{: space
bl }~ emit

\ emit positive number w/o trailing space
{: .nz
{ 0<> base c@ /mod .nz >digit }~ emit
' drop
}~|

\ emit number w/ trailing space
{: .
{ 0= ascii 0 emit }~ drop
' .nz
| }~ space

\ emit all context words
{: words
context @
{ @ 0<> dup 18 + type space }~self
'id}~|

\ Seed loop
{: seedl
words
cr
bye
}~self

7 rlabel \ the dictionary is ready here

rax A mov_rr,x
BASE rax mov_[aa],rl \ initialize numeral base
rdi 0 mov_rr,x
0 lb call \ get heap bottom
DP rax mov_[aa],rr \ initialize dictionary pointer
rbx effects @ mov_rr,aa \ initialize stack pointer
rcx xcurrent @ mov_rr,aa \ initial vocabulary
rdx SEED mov_rr,aa \ seed vocabulary pointer
rdx rcx mov_[rr],rr \ initialize it
CONTEXT rdx mov_[aa],rr \ initialize context vocabulary
CURRENT rdx mov_[aa],rr \ initialize current vocabulary

\ initialize effects
rcx x' stdemit mov_rr,aa
EFFECTS rcx mov_[aa],rr
rcx x' stdwrite mov_rr,aa
EFFECTS 8 + rcx mov_[aa],rr
rcx x' stdkey mov_rr,aa
EFFECTS 10 + rcx mov_[aa],rr
rcx x' stdinput mov_rr,aa
EFFECTS 18 + rcx mov_[aa],rr


\ execute seed loop
x' seedl jmp

0 q,

$ dup dup
f_SizeInFile @ w!
f_StringTableVA @ w!
f_StringTableOffset @ w!

0 c,

" .text" drop
" .bss" drop
" .shstrtab" drop

$ f_TotalFileSize @ w!

ELF dump

bye
v
